# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/toolchain/toolchain.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

config("ark_debugger_config") {
  cflags_cc = [
    "-fexceptions",
    "-fno-complete-member-pointers",
    "-Wno-implicit-fallthrough",
    "-fvisibility=default",
    "-frtti",
  ]
}

ohos_shared_library("ark_debugger") {
  deps = []
  defines = [
    "BOOST_ERROR_CODE_HEADER_ONLY",
    "BOOST_CLANG",
  ]

  defines += [ "ACE_LOG_TAG=\"ArkDebugger\"" ]

  if (is_mingw || is_mac) {
    cflags = [ "-std=c++17" ]
    if (is_mingw) {
      platform = "windows"
      defines += [ "WINDOWS_PLATFORM" ]
      ldflags = [
        "-lwsock32",
        "-lws2_32",
      ]
    } else {
      platform = "mac"
      defines += [
        "MAC_PLATFORM",
        "UNIX_PLATFORM",
      ]
    }
    deps = [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_$platform",
    ]
  } else if (target_os == "android") {
    defines += [
      "ANDROID_PLATFORM",
      "UNIX_PLATFORM",
    ]
    aosp_deps = [ "shared_library:liblog" ]
  } else {
    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    defines += [ "UNIX_PLATFORM" ]
  }

  include_dirs = [
    ".",
    "//third_party/boost",
    "//third_party/boost/boost",
  ]

  sources = [
    "inspector.cpp",
    "library_loader.cpp",
    "ws_server.cpp",
  ]
  if (target_os == "android") {
    sources += [ "log_wrapper.cpp" ]
  }

  configs = [ ":ark_debugger_config" ]

  part_name = "toolchain"
  subsystem_name = "arkcompiler"
}
