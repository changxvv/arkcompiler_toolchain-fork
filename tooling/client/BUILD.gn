# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/toolchain/toolchain.gni")

config("hiviewdfx_config") {
  defines = []
  if (enable_dump_in_faultlog) {
    defines += [ "ENABLE_DUMP_IN_FAULTLOG" ]
  }
  if (enable_bytrace) {
    defines += [ "ENABLE_BYTRACE" ]
    cflags_cc = [ "-Wno-gnu-zero-variadic-macro-arguments" ]
  }
  if (enable_hitrace) {
    defines += [ "ENABLE_HITRACE" ]
  }
  if (enable_hilog) {
    defines += [ "ENABLE_HILOG" ]
  }

  include_dirs = [ "$hilog_root/include" ]
}

ohos_source_set("libark_client_set") {
  stack_protector_ret = false
  defines = []
  deps = []
  configs = [ sdk_libc_secshared_config ]

  # hiviewdfx libraries
  external_deps = hiviewdfx_ext_deps
  deps += hiviewdfx_deps

  include_dirs = [
    "//arkcompiler/toolchain/inspector",
    "//arkcompiler/toolchain/websocket",
    "//arkcompiler/toolchain/tooling/base",
    "//arkcompiler/toolchain/tooling/client",
    "//third_party/cJSON",
  ]

  sources = [
    "domain/debugger_client.cpp",
    "domain/heapprofiler_client.cpp",
    "domain/profiler_client.cpp",
    "domain/runtime_client.cpp",
    "manager/domain_manager.cpp",
    "manager/variable_manager.cpp",
    "utils/utils.cpp",
    "websocket/websocket_client.cpp",
  ]

  deps += [
    "$ark_third_party_root/openssl:libcrypto_shared",
    "//arkcompiler/toolchain/tooling:libark_ecma_debugger_set",
    "//third_party/libuv:uv",
    sdk_libc_secshared_dep,
  ]

  external_deps += [ "ets_runtime:libark_jsruntime" ]

  configs += [ ":hiviewdfx_config" ]

  cflags_cc = [ "-Wno-error=vla-extension" ]

  subsystem_name = "arkcompiler"
  part_name = "toolchain"
}

ohos_shared_library("libark_client") {
  stack_protector_ret = false
  deps = [ ":libark_client_set" ]

  if (!is_mingw && !is_mac) {
    output_extension = "so"
  }

  subsystem_name = "arkcompiler"
  part_name = "toolchain"
}
